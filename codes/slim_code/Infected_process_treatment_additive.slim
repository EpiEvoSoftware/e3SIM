s7 early(){
	i_inds = which(Host_states==2);
	hosts = sim.subpopulations;
	draw_uniform = runif(length(i_inds));
	state_transition = findInterval(draw_uniform, c(0, sample_rate*sample_alive_prob, sample_rate, treatment_recovery_rate + sample_rate, treatment_recovery_rate + sample_rate + IE_rate));
	recover_from_i = i_inds[which((state_transition==1)|(state_transition==2))];
	sampled_inds = i_inds[which(state_transition<=1)];
	
	if (length(sampled_inds)>0)
	{
		sampled_inds_genomes = c();
		for (ind in sampled_inds)
		{
			sampled_pathogen = sample(hosts[ind].individuals, 1);
			line1 = paste(community.tick, ind, sampled_pathogen.tag, "I");
			writeFile(paste0(cwdir, "/sample.txt"), line1, append=T, compress=T);
			sampled_inds_genomes = c(sampled_inds_genomes, sampled_pathogen);
		}
		sim.treeSeqRememberIndividuals(sampled_inds_genomes);
	}
	
	actually_recovered = c();
	if (length(recover_from_i)>0)
	{
		for (ind in recover_from_i)
		{
			pathogens = hosts[ind].individuals;
			for (pathogen in pathogens)
			{
				muts = pathogen.genome1.mutationsOfType(1);
				treatment_failed_rate = additive_trait(muts, dr_set, cap_dr_current);

				//the bigger the effect size for drug resistance mutation is, the more probable that the treatment fails
				if (runif(1) > treatment_failed_rate)
				{
					// print(recovery_success_rate);
					line1 = paste(community.tick, ind + 1);
					writeFile(paste0(cwdir, "/recovery.txt"), line1, append=T, compress=T);
					actually_recovered = c(actually_recovered, pathogen);
				}
			}
		}
		sim.killIndividuals(actually_recovered);
		Host_states[recover_from_i[which(hosts[recover_from_i].individualCount==0)]] = 3;
	}
}