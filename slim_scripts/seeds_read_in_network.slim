1 early() {
	seeds_structure = readCSV(seed_host_matching_path, colNames=F);

	// Add N($sample_size) subpopulations (representing N humans in the population) with 0 or 1 elements (Susceptible or Infected)
	labels = c();
	seed_count = 0;
	seedsvcfpath = paste0(cwdir, "originalvcfs/");


	for (i in 1:host_size)
	{
		if (i == seeds_structure.subset(seed_count, 0) + 1)
		{
			
			sim.addSubpop(i - 1, 1, haploid=T); // Infected hosts
			sim.subpopulations[i - 1].tag = i-1;
			sim.subpopulations[i - 1].individuals.tag = seeds_structure.subset(seed_count, 1); // id of seed
			labels = c(labels, 2);
			sim.subpopulations[i - 1].genomes.readFromVCF(paste0(seedsvcfpath, "seed.", seeds_structure.subset(seed_count, 1), ".vcf"));

			if (seed_count < seed_size - 1)
			{
				seed_count = seed_count + 1;
			}
		}
		else
		{
			sim.addSubpop(i - 1, 0, haploid=T); // Susceptible
			sim.subpopulations[i - 1].tag = i - 1;
			labels = c(labels, 0);
		}
		sim.subpopulations[i - 1].setValue("Contacts", c());
	}

	// change default mutation type and selection coefficient	
	allmuts = sim.subpopulations.individuals.genome1.mutations;
	for (mut in allmuts)
	{
		if (any(mut.position == range_genes))
		{
			mut.setMutationType(1);
			which_gene = asInteger((findInterval(mut.position, trait_locs) + 1) / 2);

			for (t_num in 1:n_trans_traits)
			{
				mut.setValue(paste0("trans_", t_num), effsize_pos.subset(which_gene - 1, t_num + 2)); 
			}

			for (t_num in 1:n_dr_traits)
			{
				mut.setValue(paste0("dr_", t_num), effsize_pos.subset(which_gene - 1, t_num + 2 + n_trans_traits)); 
			}
		}
	}

	defineGlobal("Host_states", labels);
}


